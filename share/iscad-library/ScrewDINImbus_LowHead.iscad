Dn?=12;
k?=TableLookup(ScrewDINImbus_LowHead, D, Dn, k);
SW=TableLookup(ScrewDINImbus_LowHead, D, Dn, SW);
P=TableLookup(ScrewDIN, Dn, Dn, P);
dh=TableLookup(ScrewDINImbus_LowHead, D, Dn, dh);
dk=TableLookup(ScrewDINImbus_LowHead, D, Dn, dk);
d1=TableLookup(ScrewDINImbus_LowHead, D, Dn, d1);
s1i=TableLookup(ScrewDIN, Dn, Dn, s1i);
e1=TableLookup(MetricThread, P, P, e1); # Grundlochüberhang
hk=0.5*P;
L?=25;

screw0=
(( 
 Cylinder(O, -k*EX, dk)
 |
 Cylinder([0,0,0], [L,0,0], Dn)
)
- Revolution(
  Tri([L,0.5*Dn,0], [0,-P,0], [-P,0,0]),
  O, EX, 2.*M_PI))
- Extrusion(RegPoly([-k,0,0],[1,0,0],6,s1i/2.),
 [0.5*k,0,0])
;

e=screw0?edges('isCircle')?edges('minimal(CoG.x)');
screw:
Fillet(e, 0.07*dk);


Lthr?=L;
throughhole=
Cylinder(
 O,
 O+Lthr*EX,
 dh
);

Lhh?=2*L;
headhole=
Cylinder(
 O,
 O-Lhh*EX,
 d1
);

dbl=Dn-P;
blindhole=
Place(
 loadmodel(drill_hole, D=dbl, L=(L+e1), ofsTimesHtip=1, ofsTimesL=1), 
 O, -EX, EZ)
;
